{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Population and Net Migration\n",
    "\n",
    "This file computes estimates of net migration by age over the sample period 1900 to 2100. These estimates are fed to the model in a way that reconciles the Census Bureau's population historical estimates and projections with the population generated by our model through births, deaths, and migration. \n",
    "\n",
    "The file begins by amalgamating several Census Bureau products to create a consistent resident population series by single years of age over the 1900-2060 period. In addition to creating a consistent series for the total population, the notebook creates separate series of the male and female populations by single year of age, which we used to inspect trends in labor force participation rates. These latter series are called by the notebook \"Employment.ipynb\".\n",
    "\n",
    "All data sources measure the resident population as of July 1 of each calendar year. We use intercensal population statistics up to 2010, which means that Census staff adjusted the data in ways that reconcile discrepancies between decennial censuses and intermediate estimates based on growing the latest census figures through estimates of births, deaths, and migration. For the 2010-2014 data, we use the 2015 vintage of postcensal population estimates, which are based on adjustments to the 2010 Census figures based on estimates of births, deaths, and migration. Similarly, we use the latest set of population projections by age from 2014 onward based on the 2010 Census. The data sources are summarized below, along with the age coverage. We saved the raw data as CSV files and perform the data cleaning below. \n",
    "\n",
    "* [1900 to 1939] We downloaded the \"Historical National Population Estimates/National Estimates by Age, Sex, Race: 1900-1979.\" The annual data cover years of age 0 through 74 years old and 75+ years old. \n",
    "* [1940 to 1979] The source is the same as above. The annual data cover years of age 0 through 84 years old and 85+ years old.\n",
    "* [1980 to 1989] We downloaded the \"Quarterly Intercensal Resident Population\" estimates by single years of age and retained estimates as of July 1 of each year. The data cover years of age 0 through 99 years old and 99+ years old.\n",
    "* [1990 to 2000] We downloaded the \"Intercensal Estimates of the United States Population by Age and Sex, 1990-2000: All Months.\" The data cover years of age 0 through 99 years old and 99+ years old.\n",
    "* [2001 to 2013] We downloaded the \"2000-2010 Intercensal Estimates April 1, 2000 to July 1, 2010.\" The data cover years of age 0 through 84 years old and 85+ years old.\n",
    "* [2014 to 2060] We downloaded the \"2014 to 2060 Populations Projections based on Census 2010 (released 2014).\" The data cover years of age 0 through 99 years old and 100+ years old.\n",
    "\n",
    "The derivation of net migration estimates uses information on our interpolation of mortality rates from Bell and Campbell (2005). Per our timing assumptions, births, deaths, and migration take place at the beginning of the period whereas population measurement takes place at the end. Thus, we treat a population estimate for July 1 as corresponding to the population at the end of the second quarter of the calendar year. By definition, \n",
    "\n",
    "$$Population_{age,t} = \\big( Population_{age-1,t-1} + net\\_migration_{age,t} \\big) \\times (1-\\gamma_{age,t}),$$\n",
    "\n",
    "where $\\gamma_{age,t}$ is the mortality rate of an indidual aged $age$ in period $t$. As is apparent, we assume that mortality rates are the same for new immigrants as existing residents. For infants, the term $Population_{age-1,t-1}$ is replaced by our estimate of the number of live births for the period.\n",
    "\n",
    "Because the net migration statistics are computed as residuals, they may absorb sources of variation in populations statistics other than migration. These sources include some year-to-year measurement errors and imprecision in the imputation of mortality rates by age and birth cohorts.\n",
    "\n",
    "For the data beyond 2060, we assume that our estimates of net migration linearly converge to those published by the Census for 2060. These Census estimates are consistent with a stabilization in the absolute level of net migration around 2060. Our estimates and those of the Census in 2060 are similar with two minor exceptions. First, our estimated number of the net migration of infants (that is, less than a year old) is mildly negative whereas that of the Census is a bit positive, likely reflecting differences in assumptions between the UN projections that we used for projecting live births through 2100 and the assumptions underlying the Census population projections. Second, we have slightly negative net migration for older people, likely capturing differences in assumptions about mortality late in life. Both elements are unlikely to affect our macroeconomic results as they have little if any incidence on the aggregate capital-labor ratio.\n",
    "\n",
    "Due to our interpolation to a quarterly frequency of age and calendar period, our population statistics need not correspond exactly to those published by the Census Bureau. The difference is about 1/4 percent in the level of the population by 2060, indicating that the cumulative discrepancy is very small.\n",
    "\n",
    "This file also exports an estimate of the population at the end of 1899 using the oficial population counts. These official data are smoothed because there is significant over-reporting of age ending in \"0\" or \"5\" in the raw Census data, and under-reporting of the shouldering years. The information will be used as an input in the notebook `PopulationTurn1900.ipynb`, where we use the raw 1900 Census data to fill in the missing smoothed age counts. The migration counts for the elderly with missing population counts are effectively zeroed out. \n",
    "\n",
    "Historical population estimates were downloaded from https://www.census.gov/popest/data/historical/. Population projections were downloaded from https://www.census.gov/population/projections/data/national/2014.html. The pages were last accessed on August 1, 2016."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Assembling the population data from various Census publications"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Creating the array that will contain the population numbers by single year of age (0 to 119)\n",
    "population_Y_Total = zeros(120,161)\n",
    "population_Y_Men = zeros(120,161)\n",
    "population_Y_Women = zeros(120,161)\n",
    "per_year = 1900:2060\n",
    "age_year = 0:119\n",
    "\n",
    "# Adding the 1900-1979 data\n",
    "using CSV, DataFrames;\n",
    "for yy = 1900:1939 # up to 75+\n",
    "    temppop = CSV.read(\"RawData/census_pop_estimates_age_sex_\" * string(yy) * \".csv\", DataFrame; header=false, skipto=9, delim=',')\n",
    "    population_Y_Total[1:75,yy-1899] = temppop[1:75,3]\n",
    "    population_Y_Men[1:75,yy-1899] = temppop[1:75,4]\n",
    "    population_Y_Women[1:75,yy-1899] = temppop[1:75,5]\n",
    "end\n",
    "for yy = 1940:1959 # up to 85+\n",
    "    temppop = CSV.read(\"RawData/census_pop_estimates_age_sex_\" * string(yy) * \".csv\", DataFrame; header=false, skipto=9, delim=',')\n",
    "    population_Y_Total[1:85,yy-1899] = temppop[1:85,3]\n",
    "    population_Y_Men[1:85,yy-1899] = temppop[1:85,4]\n",
    "    population_Y_Women[1:85,yy-1899] = temppop[1:85,5]\n",
    "end\n",
    "for yy = 1960:1979 # change in number of header rows\n",
    "    temppop = CSV.read(\"RawData/census_pop_estimates_age_sex_\" * string(yy) *\".csv\", DataFrame; header=false, skipto=8, delim=',')\n",
    "    population_Y_Total[1:85,yy-1899] = temppop[1:85,3]\n",
    "    population_Y_Men[1:85,yy-1899] = temppop[1:85,4]\n",
    "    population_Y_Women[1:85,yy-1899] = temppop[1:85,5]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Adding the 1980s data\n",
    "for yy = 1980:1989 # change in number of header rows\n",
    "    temppop = CSV.read(\"RawData/pop_\" * string(yy) *\"_to_\" * string(yy+1) * \"_clean.csv\", DataFrame; header=false,delim=';')\n",
    "    temppop = temppop[temppop[:,2].==(yy-1200),:]\n",
    "    temppop = temppop[temppop[:,3].!=999,:]\n",
    "    population_Y_Total[1:100,yy-1899] = temppop[1:100,4]\n",
    "    population_Y_Men[1:100,yy-1899] = temppop[1:100,5]\n",
    "    population_Y_Women[1:100,yy-1899] = temppop[1:100,6]\n",
    "end\n",
    "# Adding the 1990s data (Intercensal)\n",
    "#Pkg.add(\"Missings\")\n",
    "using DataFrames, Missings\n",
    "for yy = 1990:1999 # change in number of header rows\n",
    "    temppop = CSV.read(\"RawData/intercensal_1990_2000_clean.csv\", DataFrame,skipto=4,delim=';')\n",
    "    temppop = dropmissing(temppop)\n",
    "    temppop = temppop[(temppop[:, 1] .==\"July 1\") .& (temppop[:, 2] .==yy),:] # keep July 1 data\n",
    "    temppop = temppop[temppop[:, 3].!=\"100+\",:] # keep anything that is not 100+\n",
    "    temppop = temppop[temppop[:, 3].!=\"All Age\",:] # keep anything that is not \"All Age\"\n",
    "    population_Y_Total[1:100, yy-1899] = temppop[1:100, 4]\n",
    "    population_Y_Men[1:100, yy-1899] = temppop[1:100, 5]\n",
    "    population_Y_Women[1:100, yy-1899] = temppop[1:100, 6]\n",
    "end\n",
    "# Adding the 2000s data (Intercensal)\n",
    "temppop = CSV.read(\"RawData/Population_2000_2010.csv\", DataFrame,skipto=3,delim=',')\n",
    "temppop = temppop[temppop[:,1].==7,:]\n",
    "temppop = temppop[temppop[:,3].!=85,:]\n",
    "temppop = temppop[temppop[:,3].!=999,:]\n",
    "for yy = 2000:2010 # change in number of header rows\n",
    "    population_Y_Total[1:85,yy-1899] = temppop[temppop[:,2].==yy,4]\n",
    "    population_Y_Men[1:85,yy-1899] = temppop[temppop[:,2].==yy,5]\n",
    "    population_Y_Women[1:85,yy-1899] = temppop[temppop[:,2].==yy,6]\n",
    "end\n",
    "# Adding the 2010s data\n",
    "for yy = 2010:2013 # change in number of header rows\n",
    "    temppop = CSV.read(\"RawData/pop_7_\" * string(yy) * \"_to_12_\" * string(yy) * \".csv\", DataFrame,skipto=3,delim=',')\n",
    "    temppop = temppop[temppop[:,2].==7,:]\n",
    "    temppop = temppop[temppop[:,4].!=100,:]\n",
    "    temppop = temppop[temppop[:,4].!=999,:]\n",
    "    population_Y_Total[1:100,yy-1899] = temppop[:,5]\n",
    "    population_Y_Men[1:100,yy-1899] = temppop[:,6]\n",
    "    population_Y_Women[1:100,yy-1899] = temppop[:,7]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Adding 2014:2060 projection data\n",
    "pop_2014_2060 = CSV.read(\"RawData/CensusBureau_Population_projections_2014_2060.csv\", DataFrame,skipto=2,delim=',')\n",
    "temppop=pop_2014_2060[pop_2014_2060[:,1].==0,:]\n",
    "temppop=temppop[temppop[:,2].==0,:]\n",
    "temppop=temppop[temppop[:,3].==0,:]\n",
    "temppop=transpose(Matrix(temppop[:,6:105]))\n",
    "population_Y_Total[1:100,115:161] = Array{Float64,2}(temppop)\n",
    "\n",
    "temppop=pop_2014_2060[pop_2014_2060[:,1].==0,:]\n",
    "temppop=temppop[temppop[:,2].==0,:]\n",
    "temppop=temppop[temppop[:,3].==1,:]\n",
    "temppop=transpose(Matrix(temppop[:,6:105]))\n",
    "population_Y_Men[1:100,115:161] = Array{Float64,2}(temppop)\n",
    "\n",
    "temppop=pop_2014_2060[pop_2014_2060[:,1].==0,:]\n",
    "temppop=temppop[temppop[:,2].==0,:]\n",
    "temppop=temppop[temppop[:,3].==2,:]\n",
    "temppop=transpose(Matrix(temppop[:,6:105]))\n",
    "population_Y_Women[1:100,115:161] = Array{Float64,2}(temppop);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Saving the population data to CSVs\n",
    "using DelimitedFiles\n",
    "writedlm(\"CleanData/population_1900_2060_Total.csv\",hcat([\"Age\" ; age_year],[per_year' ; population_Y_Total]), ',')\n",
    "writedlm(\"CleanData/population_1900_2060_Men.csv\",hcat([\"Age\" ; age_year],[per_year' ; population_Y_Men]), ',')\n",
    "writedlm(\"CleanData/population_1900_2060_Women.csv\",hcat([\"Age\" ; age_year],[per_year' ; population_Y_Women]), ',')\n",
    "\n",
    "# Padding population with zeros through 2100 for use below\n",
    "population_Y_Total = [population_Y_Total zeros(120,39)]\n",
    "population_Y_Men   = [population_Y_Men zeros(120,39)]\n",
    "population_Y_Women = [population_Y_Women zeros(120,39)];"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Estimating net migration flows"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Reading the annual mortality rates \n",
    "Γ_AGEy_PERy = Matrix(CSV.read(\"CleanData/interp_death_rate_1900_2220_Y.csv\", DataFrame,delim=',',header=false));\n",
    "\n",
    "# Computing the net migration flows...\n",
    "netmigration = zeros(120,200);\n",
    "netmigration[2:end,2:end] = population_Y_Total[2:end,2:end] ./ (1.0.-Γ_AGEy_PERy[2:end,2:size(netmigration,2)]) - population_Y_Total[1:end-1,1:end-1]\n",
    "\n",
    "# And adding zeros when change in population cannot be computed\n",
    "netmigration[101,:] .= 0.0;       # All years  101+\n",
    "netmigration[76,1:40] .= 0.0;     # 1900:1939  75+\n",
    "netmigration[86,41:80] .= 0.0;    # 1940-1979  85+\n",
    "netmigration[77:85,41] .= 0.0;    # 1940       75 to 85\n",
    "netmigration[87:end,81] .= 0.0;   # 1981       86+\n",
    "netmigration[86,101:110] .= 0.0;  # 2000-2010  85+\n",
    "netmigration[86:end,101] .= 0.0;  # 2001       85+\n",
    "netmigration[87:end,111] .= 0.0;  # 2010       86+\n",
    "netmigration[:,162] .= 0.0;       # 2061       All ages"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Extracting infant migration conditional on our birth series\n",
    "births = Matrix(CSV.read(\"CleanData/births_Y.csv\", DataFrame,delim=',',header=false))'\n",
    "netmigration[1,1:161]=population_Y_Total[1,1:161]./(1.0 .- Γ_AGEy_PERy[1,1:161])-births[1,1:161]\n",
    "\n",
    "# Extending the net migration series by assuming convergence to the levels Census estimates will prevail in 2060\n",
    "# Here we simply linearly converge to the migration statics in 2060, as estimated by the Census bureau. \n",
    "# Beyond that year, we hold net migration constant.\n",
    "# That way, the only population growth by 2100 comes from net migration.\n",
    "census_net_migration = Matrix(CSV.read(\"RawData/Census_net_migration_projections_2014_2060.csv\", DataFrame,skipto=2,delim=',',header=false))\n",
    "for yy=162:200\n",
    "    netmigration[:,yy]=(yy-161)/39*[census_net_migration[1:85,end] ; zeros(35,1)] + (1-(yy-161)/39)*netmigration[:,161]\n",
    "end\n",
    "\n",
    "# Because the official population data is truncated, it  makes sense to truncate the net migration data as well.\n",
    "# The conservative thing to do is set net migration to zero for 75+.\n",
    "netmigration[75:120,:] .= 0.0\n",
    "\n",
    "# Saving the net migration data to a CSV\n",
    "per_year=1900:2099\n",
    "writedlm(\"CleanData/netmigration_Y.csv\",hcat([\"Age\" ; age_year],[per_year' ; netmigration]), header = true, ',')\n",
    "writedlm(\"CleanData/netmigration_Y.csv\",netmigration, header = true, ',')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Converting the yearly net migration data to quarterly levels\n",
    "include(\"ordernorep.jl\")\n",
    "include(\"spline_cubic.jl\")\n",
    "netmigration_perQ_ageY = zeros(120,800)   # Converting periods...\n",
    "netmigration_perQ_ageQ = zeros(480,800)   # ...then converting ages\n",
    "per_quarters = 1900.25:.25:2100  # End of period so that 1900:Q2 coincides with July 1 Census population count\n",
    "per_years = 1900.5:1.0:2100\n",
    "age_years = 0.5:1:120\n",
    "age_quarters = 0.125:.25:120\n",
    "\n",
    "# Converting yearly periods to quarters and smoothing net migration statistics\n",
    "for aa=1:120\n",
    "    netmigration_perQ_ageY[aa,2:end-2] = spline_cubic(per_years,netmigration[aa,:],per_quarters[2:end-2])\n",
    "    netmigration_perQ_ageY[aa,1]=netmigration_perQ_ageY[aa,3]\n",
    "    netmigration_perQ_ageY[aa,end-1]=netmigration_perQ_ageY[aa,end-2]\n",
    "    netmigration_perQ_ageY[aa,end]=netmigration_perQ_ageY[aa,end-2]\n",
    "end\n",
    "\n",
    "# Removing the annualization\n",
    "netmigration_perQ_ageY=netmigration_perQ_ageY/4.0\n",
    "\n",
    "# Saving for inspection\n",
    "writedlm(\"CleanData/netmigration_perQ_ageY.csv\",netmigration_perQ_ageY, header = false, ',')"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Converting ages to quarters\n",
    "for qq=1:800\n",
    "    netmigration_perQ_ageQ[3:end-2,qq] = spline_cubic(age_years,netmigration_perQ_ageY[:,qq],age_quarters[3:end-2])\n",
    "end\n",
    "netmigration_perQ_ageQ[1,:]=0.5*(4*netmigration_perQ_ageY[1,:]-netmigration_perQ_ageQ[3,:]-netmigration_perQ_ageQ[3,:])\n",
    "netmigration_perQ_ageQ[2,:]=netmigration_perQ_ageQ[1,:]\n",
    "netmigration_perQ_ageQ[end-1,:]=0.5*(4*netmigration_perQ_ageY[end,:]-netmigration_perQ_ageQ[end-2,:]-netmigration_perQ_ageQ[end-3,:])\n",
    "netmigration_perQ_ageQ[end,:]=netmigration_perQ_ageQ[end-1,:]\n",
    "\n",
    "# Converting data to non-annualized values\n",
    "netmigration_perQ_ageQ=netmigration_perQ_ageQ/4\n",
    "\n",
    "# Saving to CSV\n",
    "writedlm(\"CleanData/netmigration_Q.csv\",netmigration_perQ_ageQ, header = false, ',')"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Smoothed population counts at the turn of 1900\n",
    "\n",
    "This next set of instructions compute a population at the end of 1899 up to age 74 years using the smoothed population estimates. The information will be used as an input in the notebook `PopulationTurn1900.ipynb`, where it will be merged with raw 1900 Census data to fill in the missing age counts."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Computing the population at the end of 1899 (annual) in a way\n",
    "# consistent with our net migration and mortality rate estimates in 1900\n",
    "population_1899_Y = [population_Y_Total[2:end,1]./(1.0.-Γ_AGEy_PERy[2:end,1]) ; 0]\n",
    "\n",
    "# Reading the annual mortality rates \n",
    "Γ_AGEq_PERq = Matrix(CSV.read(\"CleanData/interp_death_rate_1900_2220_Q.csv\", DataFrame,delim=',',header=false))\n",
    "\n",
    "# Computing the population at the end of 1899 (quarterly)\n",
    "population_1899_Q = zeros(480,1)\n",
    "age_quarters=0.25:0.25:120\n",
    "population_1899_Q[1:298]=spline_cubic(age_years,population_Y_Total[:,1],age_quarters[2:299])./(1.0 .- Γ_AGEq_PERq[2:299,2]).^0.25./(1.0 .- Γ_AGEq_PERq[1:298,1]).^0.25\n",
    "population_1899_Q[1:298]=population_1899_Q[1:298]-netmigration_perQ_ageQ[1:298,1] - netmigration_perQ_ageQ[2:299,2]./(1.0 .- Γ_AGEq_PERq[2:299,2]).^0.25\n",
    "population_1899_Q=population_1899_Q/4.0 # Removing annualization\n",
    "population_1899_Y[isnan.(population_1899_Y)] .= 0.0;\n",
    "\n",
    "# Saving the population to CSV (note: the 75+ years old tails need to be added)\n",
    "writedlm(\"CleanData/population_1899_smoothed_Y.csv\",hcat(age_year,population_1899_Y), header = false, ',')\n",
    "writedlm(\"CleanData/population_1899_smoothed_Q.csv\",hcat(age_quarters,population_1899_Q), header = false, ',');"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Changing permissions\n",
    "run(`chmod 664 CleanData/population_1900_2060_Total.csv`);\n",
    "run(`chmod 664 CleanData/population_1900_2060_Men.csv`);\n",
    "run(`chmod 664 CleanData/population_1900_2060_Women.csv`);\n",
    "run(`chmod 664 CleanData/netmigration_Y.csv`);\n",
    "run(`chmod 664 CleanData/netmigration_perQ_ageY.csv`);\n",
    "run(`chmod 664 CleanData/netmigration_Q.csv`);\n",
    "run(`chmod 664 CleanData/population_1899_smoothed_Y.csv`);\n",
    "run(`chmod 664 CleanData/population_1899_smoothed_Q.csv`);"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
